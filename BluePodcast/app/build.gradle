apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        signConfig {
            Properties props = new Properties()
            props.load(new FileInputStream(new File('secrets.properties')))
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['STORE_FILE'])
            storePassword props['STORE_PASSWORD']
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.example.vidbregar.bluepodcast"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [fabricApiKey: getFabricApiKey()]
        signingConfig signingConfigs.signConfig
    }
    buildTypes {
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "LISTEN_NOTES_API_KEY", "\"" + getListenNotesApiKey() + "\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "standard"
    productFlavors {
        free {
            dimension "standard"
            applicationIdSuffix ".free"
            signingConfig signingConfigs.signConfig
        }
        paid {
            dimension "standard"
            applicationIdSuffix ".paid"
            signingConfig signingConfigs.signConfig
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

static def getListenNotesApiKey() {
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['LISTEN_NOTES_API_KEY']
}

static def getFabricApiKey() {
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['FABRIC_API_KEY']
}

dependencies {
    final def ANDROID_SUPPORT_VERSION = "27.1.1"
    final def EXOPLAYER_VERSION = "2.8.2"
    final def RETROFIT_VERSION = "2.4.0"
    final def OKHTTP3_VERSION = "3.10.0"
    final def LIFECYCLE_VERSION = "1.1.1"
    final def ROOM_VERSION = "1.1.1"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$ANDROID_SUPPORT_VERSION"
    implementation "com.android.support:cardview-v7:$ANDROID_SUPPORT_VERSION"
    implementation "com.android.support:support-compat:$ANDROID_SUPPORT_VERSION"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation "com.android.support:design:$ANDROID_SUPPORT_VERSION"
    implementation "android.arch.lifecycle:extensions:$LIFECYCLE_VERSION"

    // ExoPlayer
    implementation "com.google.android.exoplayer:exoplayer-core:$EXOPLAYER_VERSION"
    implementation "com.google.android.exoplayer:exoplayer-ui:$EXOPLAYER_VERSION"

    // Room
    implementation "android.arch.persistence.room:runtime:$ROOM_VERSION"
    annotationProcessor "android.arch.persistence.room:compiler:$ROOM_VERSION"

    // Gson
    implementation 'com.google.code.gson:gson:2.8.5'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"

    // OkHttp3
    implementation "com.squareup.okhttp3:okhttp:$OKHTTP3_VERSION"
    implementation "com.squareup.okhttp3:logging-interceptor:$OKHTTP3_VERSION"

    // Butter Knife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    // RoundedImageView
    implementation 'com.makeramen:roundedimageview:2.3.0'

    // CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    // Jsoup
    implementation 'org.jsoup:jsoup:1.11.3'

    // AdMob
    freeImplementation 'com.google.android.gms:play-services-ads:15.0.1'

    // Firebase (Analytics, Crashlytics)
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'

    // JUnit
    testImplementation 'junit:junit:4.12'

    // Espresso
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply plugin: 'com.google.gms.google-services'
